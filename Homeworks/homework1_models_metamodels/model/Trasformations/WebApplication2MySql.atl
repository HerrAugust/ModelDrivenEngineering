module WebApplication2MySql;

create OUT : MySQL from IN : WebApplication;

rule WebApplication2DataBase{
	from 	s : WebApplication!WebApplicationModel
	to   	t : MySQL!DataBase (
			name <- s.name,
			tables <- s.entities		
	)
}

rule Entity2Table {
	from 	s : WebApplication!Entity
	to		t : MySQL!Table (
				name <- 'dbo.' + s.name,
				columns <- s.columns
	)
}

rule Column2Column{
	from	s : WebApplication!Column
	to		t : MySQL!Column (
		isPrimaryKey <- s.PK,
		name <- s.name,
		type <- s.type.toString(),
		comment <- 'No comment',
		defaultValue <- 'None'
	)
}

-- if there are no Private Keys => no table can refer to this table.
-- This means that, if User has attributes id and name and Page id, name and writer (writer foreignKey)
-- User - 1writes0..N - Page
-- becomes
-- User Page (without connections), where User has attributes id and name and Page id and name
rule RelatedEntity2ForeignColumn {
	from	s : WebApplication!RelatedEntity (not(s.relatesTo.columns->select(e | e.PK = true)->isEmpty()))
	to		t : MySQL!ForeignColumn (
			name 			<- s.name,
			comment 		<- 'No comment',
			type 			<- 'int',
			defaultValue 	<- '-1',
			isPrimaryKey	<- false,
			table 			<- s.refImmediateComposite(),
			refers			<- s.relatesTo -- that's an entity
	)
}
